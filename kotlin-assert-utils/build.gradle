plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.61"
    id "org.jetbrains.dokka" version "0.9.17"
    id "com.jfrog.bintray" version "1.8.4"
    id "maven-publish"
    id "java-library"
}

description = "Kotlin JUnit 4 Assert Utilities"

println "$group:${project.name}:$version"

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.1.2"
    api "junit:junit:4.12"
}

//noinspection GroovyAssignabilityCheck,GrUnresolvedAccess
task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from "$buildDir/javadoc"
}

tasks.check.dependsOn javadocJar

def info = [
        "github": "https://github.com/lovef/kotlin-assert-utils",
        "git"   : "https://github.com/lovef/kotlin-assert-utils.git",
        "issues": "https://github.com/lovef/kotlin-assert-utils/issues",
]

def mavenDir = "$buildDir/maven"

//noinspection GrUnresolvedAccess
publishing {
    publications {
        maven(MavenPublication) {
            artifact sourceJar
            artifact javadocJar
            from components.java
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name project.name
                    description description
                    url info.github
                    scm {
                        url info.github
                        connection "scm:git:${info.git}"
                        developerConnection "scm:git:${info.git}"
                    }
                    licenses {
                        license {
                            name 'Apache License Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'lovef'
                            name 'Love F'
                            email 'lovef.code@gmail.com'
                        }
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url mavenDir
        }
    }
}

//noinspection GrUnresolvedAccess
bintray {
    user = project.properties.BINTRAY_API_USER ?: System.getenv('BINTRAY_API_USER')
    key = project.properties.BINTRAY_API_KEY ?: System.getenv('BINTRAY_API_KEY')
    publications = ['maven']
    pkg {
        repo = 'maven'
        name = 'kotlin-assert-utils'
        desc = project.description
        userOrg = user
        licenses = ['Apache-2.0']
        labels = [
                "kotlin",
                "junit4"
        ]
        websiteUrl = info.github
        vcsUrl = info.git
        publish = false
        publicDownloadNumbers = true

        version {
            name = project.version
            vcsTag = gitVersion.tag
            desc = project.description
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = null //Optional. The passphrase for GPG signing'
            }
        }
    }
}
