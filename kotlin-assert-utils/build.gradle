apply plugin: 'kotlin'
apply plugin: 'maven-publish'

description = "Kotlin JUnit 4 Assert Utilities"

println "$group:${project.name}:$version"

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'junit:junit:4.12'
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

def info = [
        "github": "https://github.com/lovef/kotlin-assert-utils",
        "git"   : "https://github.com/lovef/kotlin-assert-utils.git",
        "issues": "https://github.com/lovef/kotlin-assert-utils/issues",
]

def mavenDir = "$buildDir/maven"

publishing {
    publications {
        maven(MavenPublication) {
            artifact(sourceJar)
            from components.java
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name project.name
                    description description
                    url info.github
                    scm {
                        url info.github
                        connection "scm:git:${info.git}"
                        developerConnection "scm:git:${info.git}"
                    }
                    licenses {
                        license {
                            name 'Apache License Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'lovef'
                            name 'Love F'
                            email 'lovef.code@gmail.com'
                        }
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url mavenDir
        }
    }
}

import groovy.json.JsonBuilder

//noinspection GroovyAssignabilityCheck
task bintrayDescriptor {
    doLast {
        def descriptor = [
                "package": [
                        "name"                        : "kotlin-assert-utils",
                        "repo"                        : "maven",
                        "subject"                     : "lovef",
                        "desc"                        : project.description,
                        "website_url"                 : info.github,
                        "issue_tracker_url"           : info.issues,
                        "vcs_url"                     : info.git,
                        "github_release_notes_file"   : "RELEASE.md",
                        "github_use_tag_release_notes": false,
                        "licenses"                    : [
                                "Apache-2.0"
                        ],
                        "labels"                      : [
                                "kotlin",
                                "junit4"
                        ],
                        "public_download_numbers"     : true,
                        "public_stats"                : true
                ],
                "version": [
                        "name"    : version,
                        "vcs_tag" : rootProject.getReleaseTag(),
                        "released": new Date().format("YYYY-MM-dd"),
                        "gpgSign" : true
                ],
                "files"  : [[
                        "includePattern": mavenDir + '/(.+)$',
                        "uploadPattern" : '$1'
                ]],
                "publish": false
        ]
        def bintrayDir = file("${project.buildDir}/bintray/")
        bintrayDir.mkdirs()
        file("${bintrayDir.path}/descriptor.json").text = new JsonBuilder(descriptor).toPrettyString()
    }
}

bintrayDescriptor.mustRunAfter clean

//noinspection GroovyAssignabilityCheck
task bintray(dependsOn: ['clean', 'publish', 'bintrayDescriptor']) { }
